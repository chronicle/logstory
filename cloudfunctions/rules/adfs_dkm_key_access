rule adfs_dkm_key_access {

  meta:
    author = "Google Cloud Security"
    description = "Detects access to the AD contact object to read the AD FS DKM (distributed key manager) master key value"
    reference = "https://github.com/OTRF/BHEU22-ADFS/blob/main/Slide-Deck.pdf"
    type = "alert"
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Unsecured Credentials: Private Keys"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1552/004/"
    mitre_attack_version = "v14.1"
    platform = "Windows"
    data_source = "windows security events"
    severity = "High"
    priority = "High"

  events:
    $object.metadata.event_type = "USER_RESOURCE_ACCESS"
    $object.metadata.product_name = "Microsoft-Windows-Security-Auditing"
    $object.metadata.vendor_name = "Microsoft"
    $object.metadata.product_event_type = "4662" 
    any $object.security_result.detection_fields.value = "properties:{8d3bca50-1d7e-11d0-a081-00aa006c33ed}"
    $object.principal.labels["SubjectLogonId"] = $subject
    
    $login.metadata.event_type = "USER_LOGIN"
    $login.metadata.product_name = "Microsoft-Windows-Security-Auditing"
    $login.metadata.vendor_name = "Microsoft"
    $login.metadata.product_event_type = "4624"
    $login.target.labels["TargetLogonId"] = $subject
    $login.principal.hostname = $hostname

  match:
    $hostname over 5m

  outcome:
    $risk_score = 85
    $mitre_attack_tactic = "Credential Access"
    $mitre_attack_technique = "Unsecured Credentials: Private Keys"
    $mitre_attack_technique_id = array_distinct("T1552.004")
    //added to populate alert graph with additional context
    //Commented out because it is the same as the match variable, if match variable changes, uncomment to use
    //$principal_hostname = array_distinct($logon.principal.hostname)
    $object_principal_process_pid = array_distinct($object.principal.process.pid)
    $object_principal_user_userid = array_distinct($object.principal.user.userid)
    //$principal_user_windows_sid = array_distinct($object.principal.user.windows_sid)
    $object_target_resource_name = array_distinct($object.target.resource.name)

  condition:
    $object and $login
}
